@rem Copyright (c) 2020 Antti Kivi
@rem Licensed under the Effective Elegy Licence

@echo off

set composer_name=Couplet Composer
set ode_name=Obliging Ode
set anthem_name=Unsung Anthem

echo Running the Windows batch file wrapper for %composer_name%, the build ^
script of %ode_name% and %anthem_name%
echo %composer_name% will be run in composing mode
echo The arguments passed to the script are %*

@rem Set the current directory to the source root while setting the directory constants.

set current_working_directory=%cd%

if defined ODE_SOURCE_ROOT (
  if not "%ODE_SOURCE_ROOT%"=="" (
    cd %ODE_SOURCE_ROOT%
    goto set_directories
  )
)

cd ..

goto set_directories

:set_directories
echo The working directory is set to %cd%

set build_directory_name=build
set project_directory_name=unsung-anthem

set build_directory=%cd%\%build_directory_name%
set in_tree_build_directory=%cd%\%project_directory_name%\%build_directory_name%

cd %current_working_directory%

echo The working directory is set to %cd%

set in_tree_build_option=--in-tree-build

set preset_mode_argument=preset
set compose_mode_argument=compose

set composer_executable_name=couplet-composer

@rem See if the build should be done in tree.

set in_tree_build=false

for %%g in (%*) do if /i "%%~g"=="%in_tree_build_option%" set in_tree_build=true

@rem See if the directory generated by configuring mode is in place

if not exist %build_directory% (
  if %in_tree_build_option%==false (
    echo The directory %build_directory% is missing
    goto exit_for_missing_configuration
  )
)

if not exist %in_tree_build_directory% (
  if %in_tree_build_option%==true (
    echo The directory %in_tree_build_directory% is missing
    goto exit_for_missing_configuration
  )
)

@rem Set up Couplet Composer

set current_directory=%~dp0
set current_working_directory=%cd%

call %current_directory%\util\set_up %*

@rem Change back to the directory of this script to prevent the set-up script
@rem from messing up the working directory.

cd %current_working_directory%
echo The working directory is set to %cd%

@rem Switch to the correct directory.

if defined ODE_SOURCE_ROOT (
  if not "%ODE_SOURCE_ROOT%"=="" (
    cd %ODE_SOURCE_ROOT%
    goto run_composer
  )
)

if %in_tree_build_option%==false (
  cd ..
)

set ODE_SOURCE_ROOT=%cd%

goto run_composer

@rem Run Couplet Composer

:run_composer
set preset_mode=false

if "%~1"=="%preset_mode_argument%" set preset_mode=true

if %preset_mode%==true (
  start "%composer_name%" /w /b pipenv run %composer_executable_name% %* ^
%compose_mode_argument%
) else (
  start "%composer_name%" /w /b pipenv run %composer_executable_name% ^
%compose_mode_argument% %*
)

goto :eof

:exit_for_missing_configuration
echo The build directory wasn't found; please make sure to run the cofiguring ^
script before attempting to build %ode_name% and %anthem_name% 1>&2
exit 1
